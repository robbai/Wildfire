// This file controls gradle, which we are using to install and update the RLBot framework used by this bot,
// and also compile and run the java code used by this bot.

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
    	// Spotless.
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.27.2"
    }
}

repositories {
    mavenCentral()
    jcenter()

    // Jitpack (used to pull Jamepad)
    maven { url "https://jitpack.io" }
}

mainClassName = 'wildfire.Main'

// This directory will be created and the interface dll copied into it at runtime.
// The end result is that the interface dll will be available for loading.
def dllDirectory = 'build/dll'
applicationDefaultJvmArgs = ["-Djna.library.path=" + dllDirectory]

// Spotless.
apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin
spotless {
    java {
        importOrderFile './spotless/importorder.importorder'  // An import ordering file, exported from Eclipse
        removeUnusedImports()                                 // Removes any unused imports
        eclipse().configFile './spotless/format.xml'          // XML file dumped out by the Eclipse formatter
    }
}

dependencies {
    // Fetch the framework jar file
    compile 'org.rlbot.commons:framework:1.+'

    // This is makes it easy to find the dll when running in intellij, where JVM args don't get passed from gradle.
    runtime files(dllDirectory)
    
    // Jamepad
    compile 'com.github.WilliamAHartman:Jamepad:1.3.2'
	
    // javagl's Obj
    compile group: 'de.javagl', name: 'obj', version: '0.3.0'
	
	// Apache Commons CSV
	compile "org.apache.commons:commons-csv:1.5"
}

task checkPipUpgradeSafety {
    doLast {
        new ByteArrayOutputStream().withStream { os ->
            def exitValue = exec {
                commandLine "python", "-c", "from rlbot.utils import public_utils; print(public_utils.is_safe_to_upgrade());"
                standardOutput = os
                ignoreExitValue = true
            }.exitValue

            // If the exit value is nonzero, the command  probably failed because rlbot is not installed at all.
            ext.isSafe = exitValue != 0 || os.toString().trim() == "True"
        }
    }
}


// Uses pip (the python package manager) to install all the python packages needed for this bot, as defined
// in requirements.txt.
task pipInstallRequirements {
    dependsOn 'checkPipUpgradeSafety'

    doLast {
        if (checkPipUpgradeSafety.isSafe) {
            exec {
                commandLine "python", "-m", "pip", "install", "-r", "requirements.txt", "--upgrade"
            }
        } else {
            println 'Skipping upgrade attempt because files are in use.'
        }
    }
}

task createDllDirectory {
    mkdir dllDirectory
}

// Installs or updates RLBot. Empty task for now. It still does stuff because it "dependsOn" tasks that do things.
task updateRLBot {
    dependsOn 'pipInstallRequirements'
    dependsOn 'createDllDirectory'
}
updateRLBot.dependsOn pipInstallRequirements

applicationDistribution.exclude(dllDirectory)

// You can run gradew.bat distZip to generate a zip file suitable for tournament submissions.
// It will be generated in build/distributions
distZip {
    into ('python') {
        from fileTree('src/main/python') {
            exclude '__pycache__'
            exclude 'wildfireTest.cfg'
			exclude 'grabby.cfg'
        }
    }
}

// This is the same as distZip, but not zipped. Handy for testing your tournament submission more rapidly.
installDist {
    into ('../python') {
        from fileTree('src/main/python') {
            exclude '__pycache__'
			exclude 'wildfireTest.cfg'
			exclude 'grabby.cfg'
        }
    }
}